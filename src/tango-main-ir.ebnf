!whitespace : ' ' | '\t' | '\n' | '\r' ;
!comment : _lineComment | _blockComment ;
_lineComment : '/' '/' {.} '\n' ;
_blockComment : '/' '*' {. | '*'} '*' '/' ;

/* Production Rules */

_newline      : '\x0A' ;
_ascii_char   : '\x00'-'\x09' | '\x0B'-'\x21' | '\x23'-'\x26' | '\x28'-'\x59' | '\x61'-'\xFF' ;
_ascii_letter : 'a'-'z' | 'A'-'Z' ;
_ascii_digit  : '0'-'9' ;

_letter                : _ascii_letter | '_' ;
_decimal_digit_wo_zero : '1'-'9' ;
_decimal_digit         : '0' | _decimal_digit_wo_zero ;
_octal_digit           : '0' - '7' ;
_hex_digit             : '0' - '9' | 'A' - 'F' | 'a' - 'f' ;

_decimal_lit : _decimal_digit_wo_zero { _decimal_digit } ;
_octal_lit   : '0' { _octal_digit } ;
_hex_lit     : '0' ( 'x' | 'X' ) _hex_digit { _hex_digit } ;

_decimals  : _decimal_digit { _decimal_digit } ;
_exponent  : ( 'e' | 'E' ) [ '+' | '-' ] _decimals ;

_ascii_value_rune   : _ascii_char | '"'  | '`'  | _escaped_char ;
_ascii_value_string : _ascii_char | '\'' | '`'  | _escaped_char ;
_byte_value         : _octal_byte_value | _hex_byte_value ;
_octal_byte_value   : '\\' _octal_digit _octal_digit _octal_digit ;
_hex_byte_value     : '\\' 'x' _hex_digit _hex_digit ;
_escaped_char       : '\\' ( 'a' | 'b' | 'f' | 'n' | 'r' | 't' | 'v' | '\\' | '"' | '\'' ) ;

_raw_string_lit         : '`' { _ascii_char | '"' | '\'' | _newline } '`' ;
_interpreted_string_lit : '"' { _ascii_value_string | _byte_value } '"' ;

/* Lexical Part */

keyword_break      : 'b' 'r' 'e' 'a' 'k' ;
keyword_default    : 'd' 'e' 'f' 'a' 'u' 'l' 't' ;
keyword_func       : 'f' 'u' 'n' 'c' ;
keyword_interface  : 'i' 'n' 't' 'e' 'r' 'f' 'a' 'c' 'e' ;
keyword_case       : 'c' 'a' 's' 'e' ;
keyword_defer      : 'd' 'e' 'f' 'e' 'r' ;
keyword_map        : 'm' 'a' 'p' ;
keyword_struct     : 's' 't' 'r' 'u' 'c' 't' ;
keyword_else       : 'e' 'l' 's' 'e' ;
keyword_goto       : 'g' 'o' 't' 'o' ;
keyword_package    : 'p' 'a' 'c' 'k' 'a' 'g' 'e' ;
keyword_switch     : 's' 'w' 'i' 't' 'c' 'h' ;
keyword_const      : 'c' 'o' 'n' 's' 't' ;
keyword_fallthrough: 'f' 'a' 'l' 'l' 't' 'h' 'r' 'o' 'u' 'g' 'h' ;
keyword_if         : 'i' 'f' ;
keyword_range      : 'r' 'a' 'n' 'g' 'e' ;
keyword_type       : 't' 'y' 'p' 'e' ;
keyword_continue   : 'c' 'o' 'n' 't' 'i' 'n' 'u' 'e' ;
keyword_for        : 'f' 'o' 'r' ;
keyword_import     : 'i' 'm' 'p' 'o' 'r' 't' ;
keyword_return     : 'r' 'e' 't' 'u' 'r' 'n' ;
keyword_var        : 'v' 'a' 'r' ;

identifier         : _letter { _letter | _ascii_digit } ;

int_lit            : _decimal_lit | _octal_lit | _hex_lit ;
float_lit          : _decimals '.' [ _decimals ] [ _exponent ] | _decimals _exponent | '.' _decimals [ _exponent ] ;
rune_lit           : '\'' ( _ascii_value_rune | _byte_value ) '\'' ;
string_literal     : _raw_string_lit | _interpreted_string_lit ;

rel_op     : '=' '=' | '!' '=' | '<' '=' | '>' '=' | '>' | '<' ;
mul_op     : '/' | '%' | '<' '<' | '>' '>' | '&' '^' ;
unary_op   : '!' ;

inc_dec_op  : '+' '+' | '-' '-' ;

norm_assign_op  : '=' ;
mod_assign_op   : '|' '=' | '+' '=' | '-' '=' | '^' '='
                | '*' '=' | '/' '=' | '%' '=' | '<' '<' '=' | '>' '>' '=' | '&' '=' | '&' '^' '=';
auto_assign_op  : ':' '=' ;

spread_op : '.' '.' '.' ;

left_block_bracket : '{' ;
right_block_bracket: '}' ;
left_paren         : '(' ;
right_paren        : ')' ;
left_sq_paren      : '[' ;
right_sq_paren     : ']' ;
label_op           : ':' ;
stmt_end           : ';' ;
comma              : ',' ;
dot                : '.' ;
star               : '*' ;
and                : '&' ;
plus               : '+' ;
minus              : '-' ;
xor                : '^' ;
simple_pipe        : '|' ;
andand             : '&' '&' ;
oror               : '|' '|' ;
left_angle         : '<' '<' '<' ;
right_angle        : '>' '>' '>' ;
left_banana        : '[' '(' ;
right_banana       : ')' ']' ;
left_tuple         : '(' '|' ;
right_tuple        : '|' ')' ;
pipe               : '|' '|' '|';

/* Syntax Part */

<<

  import "tango/src/token"
  import "tango/src/ast"

>>

SourceFile    : PackageClause Imports DeclList  << ast.NewSourceFile($2) >>
              ;

PackageClause : keyword_package identifier stmt_end
              ;

Imports    : empty
           | Imports Import stmt_end
           ;

Import     : keyword_import ImportStmt
           | keyword_import left_paren ImportStmtList OSemi right_paren
           | keyword_import left_paren right_paren
           ;

ImportStmt : ImportHere string_literal
           ;

ImportStmtList : ImportStmt
               | ImportStmtList stmt_end ImportStmt
               ;

ImportHere : empty
           | identifier
           | dot
           ;

/* Declarations */
Declaration : CommonDecl  << $0, nil >>
            | FuncDecl    << $0, nil >>
            | NonDeclStmt << $0, nil >>
//            | error
            ;

CommonDecl : keyword_const ConstDecl                                                      << $1, nil >>
           | keyword_const left_paren ConstDecl OSemi right_paren	                        << $2, nil >>
           | keyword_const left_paren ConstDecl stmt_end ConstDeclList OSemi right_paren	<< ast.MultConstDecl($2, $4) >>
           | keyword_const left_paren right_paren                                         << &ast.AddrCode{}, nil >>
           | keyword_var VarDecl                                                          << $1, nil >>
           | keyword_var left_paren VarDeclList OSemi right_paren                         << ast.MergeCodeList($2) >>
           | keyword_var left_paren right_paren                                           << &ast.AddrCode{}, nil >>
           | keyword_type TypeDecl                                                        << $1, nil >>
           | keyword_type left_paren TypeDeclList OSemi right_paren                       << $2, nil >>
           | keyword_type left_paren right_paren                                          << &ast.AddrCode{}, nil >>
           ; 

VarDecl   : DeclNameList NType                                                            << ast.Decl($0, $1, nil, false) >>
          | DeclNameList NType norm_assign_op RVal ExprList LVal                          << ast.Decl($0, $1, $4, false) >>
          | DeclNameList norm_assign_op ExprList                                          << nil, ast.ErrUnsupported >>
          ;

ConstDecl : DeclNameList NType norm_assign_op RVal ExprList LVal                          << ast.Decl($0, $1, $4, true) >>
          | DeclNameList norm_assign_op ExprList                                          << nil, ast.ErrUnsupported >>
          ;

ConstDecl1 : ConstDecl	        << $0, nil >>
           | DeclNameList NType	<< nil, ast.ErrUnsupported >>
           | DeclNameList	      << nil, ast.ErrUnsupported >>
           ;

TypeDeclName : identifier	      << $0, nil >>
             ;

TypeDecl : TypeDeclName NType	  << ast.EvalTypeDecl($0, $1) >>
         ;

// TODO: Think about addressable left hand side
SimpleStmt : Expr                                        << $0, nil >>
           | Expr mod_assign_op RVal Expr LVal           << ast.ModAssignment($0, $1, $3) >>
           | ExprList norm_assign_op RVal ExprList LVal	 << ast.Assignments($0, $3) >>
           | ExprList auto_assign_op ExprList	           << nil, ast.ErrUnsupported >>
           | Expr inc_dec_op	                           << ast.IncDec($0, $1) >>
           ;

LVal : empty << ast.LVal() >>
     ;

RVal : empty << ast.RVal() >>
     ;

Case : keyword_case ExprOrTypeList label_op	                      << $1, nil >>
//     | keyword_case ExprOrTypeList norm_assign_op Expr label_op	<< ast.AddNode("Case", $0, $1, $2, $3, $4) >>
//     | keyword_case ExprOrTypeList auto_assign_op Expr label_op	<< ast.AddNode("Case", $0, $1, $2, $3, $4) >>
     | keyword_default label_op	                                  << []*ast.AddrCode{}, nil >>
     ;

CompoundStmt : left_block_bracket NewScope StmtList EndScope right_block_bracket	<< ast.MergeCodeList($2) >>
             ;

NewScope     : empty << ast.NewScope() >>
             ;

EndScope     : empty << ast.EndScope() >>
             ;

CaseBlock : Case StmtList	                                       << ast.EvalCaseDecl($0, $1, false) >>
          | Case StmtList stmt_end keyword_fallthrough stmt_end  << ast.EvalCaseDecl($0, $1, true) >>
          ;

CaseBlockList : empty	                  << ast.NewCaseBlockList() >>
              | CaseBlockList CaseBlock	<< ast.AddToCaseBlockList($0, $1) >>
              ;

LoopBody : left_block_bracket NewScope StmtList EndScope right_block_bracket	<< ast.MergeCodeList($2) >>
         ;

RangeStmt : ExprList norm_assign_op keyword_range Expr	<< ast.AddNode("RangeStmt", $0, $1, $2, $3) >>
          | ExprList auto_assign_op keyword_range Expr	<< ast.AddNode("RangeStmt", $0, $1, $2, $3) >>
          | keyword_range Expr	                        << ast.AddNode("RangeStmt", $0, $1) >>
          ;

ForHeader : OSimpleStmt stmt_end OExpr stmt_end OSimpleStmt << ast.EvalForHeader($0, $2, $4) >>
          | OExpr     	                                    << ast.EvalForHeader(&ast.AddrCode{}, $0, &ast.AddrCode{}) >>
          | RangeStmt	                                      << nil, ast.ErrUnsupported >>
          ;

ForBody : ForHeader LoopBody	                        << ast.EvalForBody($0, $1) >>
        ;

ForStmt : keyword_for NewScope ForBody EndScope	              << $2, nil >>
        ;

IfHeader : Expr       	                              << ast.EvalIfHeader(&ast.AddrCode{}, $0) >>
         | OSimpleStmt stmt_end Expr       	          << ast.EvalIfHeader($0, $2) >>
         ;

IfStmt : keyword_if NewScope IfHeader LoopBody ElseIfList Else EndScope << ast.EvalIf($2, $3, $4, $5) >>
       ;

ElseIf : keyword_else keyword_if NewScope IfHeader LoopBody EndScope  << ast.EvalElseIf($3, $4) >>
       ;

ElseIfList : empty                                    << ast.NewIfElseList(nil) >>
           | ElseIfList ElseIf                        << ast.AddToIfElseList($0, $1) >>
           ;

Else : empty	                                        << &ast.AddrCode{}, nil >>
     | keyword_else CompoundStmt	                    << $1, nil >>
     ;

NType : FuncType                      << nil, ast.ErrUnsupported >>
      |	OtherType	                    << $0, nil >>
      |	PtrType	                      << $0, nil >>
      |	DotName	                      << $0, nil >>
      |	left_paren NType right_paren	<< $1, nil >>
      ;

NonExprType : FuncType	        << nil, ast.ErrUnsupported >>
            | OtherType	        << $0, nil >>
            | star NonExprType	<< ast.EvalPtrType($1) >>
            ;

OtherType : left_sq_paren OExpr right_sq_paren NType	            << ast.EvalArrType($1, $3) >>
          | keyword_map left_sq_paren NType right_sq_paren NType	<< nil, ast.ErrUnsupported >>
          | StructType	                                          << $0, nil >>
          | InterfaceType                                         << nil, ast.ErrUnsupported >>
          ;

StructType : keyword_struct left_block_bracket StructDeclList OSemi right_block_bracket	<< ast.EvalStructType($2) >>
           | keyword_struct left_block_bracket right_block_bracket	                    << ast.EvalStructType(nil) >>
           ;

InterfaceType : keyword_interface left_block_bracket InterfaceDeclList OSemi right_block_bracket << ast.AddNode("InterfaceType", $0, $1, $2, $3, $4) >>
              | keyword_interface left_block_bracket right_block_bracket                         << ast.AddNode("InterfaceType", $0, $1, $2) >>
              ;

/* Function Stuff - All together to show how crappy it is */
FuncDecl : keyword_func FuncDecl_ FuncBody EndScope                      << ast.FuncDecl($1, $2) >>
         ;

FuncDecl_ : identifier ArgList FuncRes                                            << ast.FuncSign($0, $1, $2, nil) >>
          | left_tuple OArgTypeListOComma right_tuple identifier ArgList FuncRes  << ast.FuncSign($3, $4, $5, $1) >>
          ;

FuncType : keyword_func ArgList FuncRes                                            << ast.FuncType($1, $2) >>
         ;

ArgList : left_paren OArgTypeListOComma right_paren	        << $1, nil >>
        | ArgList left_paren OArgTypeListOComma right_paren	<< nil, ast.ErrUnsupported >>
        ;

FuncBody : empty                                            << &ast.AddrCode{}, nil >>
         | left_block_bracket StmtList right_block_bracket  << ast.MergeCodeList($1) >>
         ;

FuncRes : empty                                     << ast.VoidType, nil >>
        | FuncRetType                               << $0, nil >>
        | left_tuple OArgTypeListOComma right_tuple << nil, ast.ErrUnsupported >>
        ;

StructDeclList : StructDecl	                        << ast.NewStructDeclList($0) >>
               | StructDeclList stmt_end StructDecl	<< ast.AddToStructDeclList($0, $2) >>
               ;

InterfaceDeclList : InterfaceDecl	                            << ast.AddNode("InterfaceDeclList", $0) >>
                  | InterfaceDeclList stmt_end InterfaceDecl	<< ast.AddNode("InterfaceDeclList", $0, $1, $2) >>
                  ;

StructDecl : IdList NType             	                << ast.EvalStructIDList($0, $1) >>
           | Embed                                      << ast.EvalStructEmbed($0) >>
           ;

IdList     : identifier                                 << ast.NewIdentifierList($0) >>
           | IdList comma identifier                    << ast.AddToIdentifierList($0, $1) >>
           ;

InterfaceDecl : NewName InDecl                    << ast.AddNode("InterfaceDecl", $0, $1) >>
              | identifier                        << ast.AddNode("InterfaceDecl", $0) >>
              | left_paren identifier right_paren	<< ast.AddNode("InterfaceDecl", $0, $1, $2) >>
              ;

InDecl : left_paren OArgTypeListOComma right_paren FuncRes  << ast.AddNode("InDecl", $0, $1, $2, $3) >>
       ;

LabelName : identifier   << ast.EvalLabelName($0) >>
          ;

PtrType : star NType	   << ast.EvalPtrType($1) >>
        ;

FuncRetType : FuncType  << nil, ast.ErrUnsupported >>
            | OtherType << $0, nil >>
            | PtrType   << $0, nil >>
            | DotName   << $0, nil >>
            ;

DotName : Name                << ast.IsType($0) >>
        | Name dot identifier << nil, ast.ErrUnsupported >>
        ;

OComma : empty
       | comma
       ;

OSemi : empty
      | stmt_end
      ;

OSimpleStmt : empty	      << &ast.AddrCode{}, nil >>
            | SimpleStmt	<< $0, nil >>
            ;

OName    : empty    << nil, nil >>
         | Name     << $0, nil >>
         ;

OExpr : empty	<< nil, nil >>
      | Expr	<< $0, nil >>
      ;

OExprList : empty     << ast.NewList(nil) >>
          | ExprList  << $0, nil >>
          ;

FuncLiteralDecl : FuncType	<< ast.AddNode("FuncLiteralDecl", $0) >>
                ;

FuncLiteral : FuncLiteralDecl left_block_bracket StmtList right_block_bracket	<< ast.AddNode("FuncLiteral", $0, $1, $2, $3) >>
//            | FuncLiteralDecl error
            ;

ExprList : Expr                 << ast.NewList($0) >>
         | ExprList comma Expr  << ast.AddToList($0, $2) >>
         ;

ExprOrTypeList : ExprOrType                       << ast.NewList($0) >>
               | ExprOrTypeList comma ExprOrType  << ast.AddToList($0, $2) >>
               ;


Literal : int_lit         << ast.IntLit($0) >>
        | float_lit	      << nil, ast.ErrUnsupported >>
        | rune_lit	      << nil, ast.ErrUnsupported >>
        | string_literal  << ast.StringLit($0) >>
        ;

Embed : Name	<< $0, nil >>
      ;

DeclList : empty                          << ast.NewList(nil) >>
         | DeclList Declaration stmt_end  << ast.AddToList($0, $1) >>
         ;

VarDeclList : VarDecl                       << ast.NewList($0) >>
            | VarDeclList stmt_end VarDecl  << ast.AddToList($0, $2) >>
            ;

ConstDeclList : ConstDecl1                        << ast.NewList($0) >>
              | ConstDeclList stmt_end ConstDecl1 << ast.AddToList($0, $2) >>
              ;

TypeDeclList : TypeDecl                       << $0, nil >>
             | TypeDeclList stmt_end TypeDecl << $2, nil >>
             ;

DeclNameList : DeclName                     << ast.NewList($0) >>
             | DeclNameList comma DeclName  << ast.AddToList($0, $2) >>
             ;

StmtList : Stmt                    << ast.NewList($0) >>
         | StmtList stmt_end Stmt  << ast.AddToList($0, $2) >>
         ;

KeyvalList : Keyval	                          << ast.NewKeyvalList($0) >>
           | BareCompLitExpr	                << ast.NewList($0) >>
           | KeyvalList comma Keyval	        << ast.AddToKeyvalList($0, $2) >>
           | KeyvalList comma BareCompLitExpr	<< ast.AddToList($0, $2) >>
           ;

BracedKeyvalList : empty	            << nil, nil >>
                 | KeyvalList OComma	<< $0, nil >>
                 ;

DeclName : NewName  << $0, nil >>
         ;

NewName : identifier << ast.NewName($0) >>
        ;

Name : identifier	<< ast.Name($0) >>
     ;

ArgType : identifier NameOrType << ast.EvalArgType($0, $1) >>
        | identifier DotDotDot  << nil, ast.ErrUnsupported >>
        | DotDotDot             << nil, ast.ErrUnsupported >>
        | NameOrType	          << nil, ast.ErrUnsupported >>
        ;

ArgTypeList : ArgType                   << ast.NewArgTypeList($0) >>
            | ArgTypeList comma ArgType << ast.AddToArgTypeList($0, $2) >>
            ;

OArgTypeListOComma : empty              << ast.NewArgTypeList(nil) >>
                   | ArgTypeList OComma	<< $0, nil >>
                   ;

/* Statement */
Stmt : empty	      << &ast.AddrCode{}, nil >>
     | CompoundStmt	<< $0, nil >>
     | CommonDecl	  << $0, nil >>
     | NonDeclStmt	<< $0, nil >>
//     | error
     ;

NonDeclStmt : SimpleStmt	              << $0, nil >>
            | ForStmt	                  << $0, nil >>
            | SwitchStmt	              << $0, nil >>
            | IfStmt	                  << $0, nil >>
            | keyword_break OName	      << ast.EvalBreak() >>
            | keyword_continue OName	  << ast.EvalContinue() >>
            | LabelName label_op Stmt	  << ast.EvalLabel($0, $2) >>
            | keyword_goto Name         << ast.EvalGoto($1) >>
            | keyword_return OExprList  << ast.EvalReturn($1) >>
            | keyword_defer PseudoCall  << nil, ast.ErrUnsupported >>
            ;

DotDotDot : spread_op                     << ast.AddNode("DotDotDot", $0) >>
          | spread_op NType               << ast.AddNode("DotDotDot", $0, $1) >>
          ;

PExpr : PExprNoParen                      << $0, nil >>
      | left_paren ExprOrType right_paren << $1, nil >>
      ;

PExprNoParen : Literal                                                                << $0, nil >>
             | PseudoCall                                                             << $0, nil >>
             | Name                                                                   << ast.ProcessName($0) >>
             | PExpr dot identifier	                                                  << ast.EvalStructAccess(ast.EvalWrapped($0), $2) >>
             | PExpr left_sq_paren Expr right_sq_paren	                              << ast.EvalArrAccess(ast.EvalWrapped($0), $2) >>
             | PExpr left_sq_paren OExpr label_op OExpr right_sq_paren	              << ast.EvalArrSlice($0, $2, $4) >>
             | CompType left_banana BracedKeyvalList right_banana                     << ast.EvalCompType($0, $2) >>
             | PExpr left_sq_paren OExpr label_op OExpr label_op OExpr right_sq_paren	<< nil, ast.ErrUnsupported >>
             | PExpr left_banana BracedKeyvalList right_banana                        << nil, ast.ErrUnsupported >>
             | ConvType left_angle Expr OComma right_angle                            << nil, ast.ErrUnsupported >>
             | ForCompExpr                                                            << nil, ast.ErrUnsupported >>
             | FuncLiteral                                                            << nil, ast.ErrUnsupported >>
             | PExpr dot left_paren ExprOrType right_paren	                          << nil, ast.ErrUnsupported >>
             | PExpr dot left_paren keyword_type right_paren	                        << nil, ast.ErrUnsupported >>
             ;

ConvType : FuncType   << nil, ast.ErrUnsupported >>
         | OtherType  << $0, nil >>
         ;

CompType : OtherType  << $0, nil >>
         ;

Keyval : identifier label_op CompLitExpr  << ast.EvalKeyval($0, $2) >>
       ;

BareCompLitExpr : Expr                                      << $0, nil >>
                | left_banana BracedKeyvalList right_banana << nil, ast.ErrUnsupported >>
                ;

CompLitExpr : Expr                                      << $0, nil >>
            | left_banana BracedKeyvalList right_banana << nil, ast.ErrUnsupported >>
            ;

ExprOrType : Expr         << $0, nil >>
           | NonExprType  << $0, nil >>
           ;

NameOrType : NType        << $0, nil >>
           ;

SwitchStmt : keyword_switch NewScope IfHeader left_block_bracket CaseBlockList EndScope right_block_bracket	<< ast.EvalSwitch($2, $4) >>
           ;
           
/* Expressions */
// My fix for binary_op SR Conflict might be wrong. Double Check Please!!!

Prec5Expr_ : UExpr	                            << $0, nil >>
           | Prec5Expr_ mul_op UExpr	          << ast.BinaryOp($0, $1, $2) >>
           | Prec5Expr_ and UExpr	              << ast.BinaryOp($0, $1, $2) >>
           | Prec5Expr_ star UExpr	            << ast.BinaryOp($0, $1, $2) >>
           ;

Prec4Expr_ : Prec5Expr_	                        << $0, nil >>
           | Prec4Expr_ plus Prec5Expr_	        << ast.BinaryOp($0, $1, $2) >>
           | Prec4Expr_ minus Prec5Expr_	      << ast.BinaryOp($0, $1, $2) >>
           | Prec4Expr_ xor Prec5Expr_          << ast.BinaryOp($0, $1, $2) >>
           | Prec4Expr_ simple_pipe Prec5Expr_	<< ast.BinaryOp($0, $1, $2) >>
           ;

Prec3Expr_ : Prec4Expr_	                        << $0, nil >>
           | Prec3Expr_ rel_op Prec4Expr_	      << ast.RelOp($0, string($1.(*token.Token).Lit), $2) >>
           ;

Prec2Expr_ : Prec3Expr_	                        << $0, nil >>
           | Prec2Expr_ andand Prec3Expr_	      << ast.AndOp($0, $2) >>
           ;

Expr       : Prec2Expr_	                        << $0, nil >>
           | Expr oror Prec2Expr_	              << ast.OrOp($0, $2) >>
           ;

UExpr : PExpr	                                  << $0, nil >>
      | unary_op UExpr	                        << ast.UnaryOp($0, $1) >>
      | star UExpr                              << ast.UnaryOp($0, ast.EvalWrapped($1)) >>
      | and UExpr                               << ast.UnaryOp($0, $1) >>
      | plus UExpr	                            << ast.UnaryOp($0, $1) >>
      | minus UExpr	                            << ast.UnaryOp($0, $1) >>
      | xor UExpr	                              << ast.UnaryOp($0, $1) >>
      ;

StarPP : empty << ast.StarPP() >>
       ;

StarMM : empty << ast.StarMM() >>
       ;

ForCompExpr : left_sq_paren Expr pipe RangeStmt right_sq_paren	<< ast.AddNode("ForCompExpr", $0, $1, $2, $3, $4) >>
            ;

/* Call-Like Statements that can be preceded by 'defer' */
PseudoCall : PExpr left_paren right_paren	                                << ast.EvalCall($0, []*ast.AddrCode{}) >>
           | PExpr left_paren ExprOrTypeList OComma right_paren	          << ast.EvalCall($0, $2) >>
           | PExpr left_paren ExprOrTypeList spread_op OComma right_paren	<< nil, ast.ErrUnsupported >>
           ;